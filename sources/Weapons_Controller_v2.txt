@name Weapons Controller v2
@inputs Gun:wirelink

#[
    Author: TJ Peden
    License: MIT
]#

if( first() ) {
    dsJoinGroup( "Weapons" )

    #include "sources/lib/constants"

    printColor( Red, "Name me, please" )
}

if( !->Gun ) { exit() }

if( dsClk( "Activate" ) ) {
    local Active = dsGetNumber()

    Gun["Open",   number] = Active
    Gun["Active", number] = Active
}

if( dsClk( "Target" ) ) {
    local Target = dsGetEntity()
    if( Gun:hasInput( "Target" ) ) { Gun["Target", entity] = Target }
    if( Gun:hasInput( "Entity" ) ) { Gun["Entity", entity] = Target }

    if( Gun:hasInput( "Lock" ) ) {
        Gun["Lock", number] = Target:isValid()
    }

    dsSendDirect( "Target.Aquired", dsGetSender(), Target:isValid() )
}

if( Gun:hasInput( "Fire" ) ) {
    if( dsClk( "Fire" ) ) {
        Gun["Fire", number] = dsGetNumber()
    }
}

if( Gun:hasInput( "Launch" ) ) {

    if( dsClk( "Target.Position" ) ) {
        Gun["Vector", vector] = dsGetVector()
    }

    if( dsClk( "Fire" ) ) {
        Gun["Launch", number] = dsGetNumber()
    }
}
