@name Weapons Chair
@inputs Gun:wirelink Pod:wirelink Cam:wirelink
@outputs Gunner AimPos:vector Target:entity
@persist Mouse1 Mouse2 Space Shift Alt
@persist Pos:vector Target:entity
@persist Lock Active Gunner
@persist On Off Hold Press Release Current Last

interval(1)

if( first() ) {
    Off = 0
    On  = !Off
    
    Hold    = 3
    Press   = 2
    Release = 1
    
    Current = 2
    Last    = 1
}

Active = Pod["Active", number]
Shift  = Pod["Shift",  number]

if( !Active ) {
    Gunner = Off
    Lock = Off
    Cam["Activated", number] = Gunner
    Gun["Active",    number] = Gunner
    Gun["Open",      number] = Gunner
    exit()
}
elseif( Active & $Active ) {
    E = Pod["Entity", entity]
    Pos = E:driver():pos() + vec(0, 0, 120)
    Pod["Crosshairs", number] = Off
    Cam["Parent", entity] = E
}

Mouse1 = ( Mouse1 >> 1 ) + 2 * Pod["Mouse1", number]
Mouse2 = ( Mouse2 >> 1 ) + 2 * Pod["Mouse2", number]
Alt    = ( Alt    >> 1 ) + 2 * Pod["Alt",    number]

if( Alt == Press ) {
    Gunner = !Gunner
    Cam["Activated", number] = Gunner
    Gun["Active",    number] = Gunner
    Gun["Open",      number] = Gunner
}

if( Gunner ) {
    Angle = ang( -Pod["Elevation", number], Pod["Bearing", number], 0 )
    Cam["Angle", angle] = Angle
    
    if( Pod["W", number] | Pod["S", number] ) {
        OS = vec(1, 0, 0):rotate(Angle:setPitch(-Angle:pitch()))
        Pos += OS:setZ(-OS:z()) * (10 - (Pod["S", number] * 2) + (Shift * 9)) * (Pod["W", number] - Pod["S", number])
    }
    if( Pod["A", number] | Pod["D", number] ) {
        OS = vec(1, 0, 0):rotate(Angle + ang(-Angle:pitch(), 90, 0))
        Pos += OS:setZ(-OS:z()) * (6 + (Shift * 8)) * (Pod["A", number] - Pod["D", number])
    }
    if( Pod["Space", number] ) {
        Pos += vec(0, 0, 6 + (Shift * 8))
    }
    if( Pod["R", number] ) {
        E = Pod["Entity", entity]
        Pos = E:driver():pos() + vec(0, 0, 120)
    }
    
    Cam["Position", vector] = Pos
    
    AD = vec(1, 0, 0):rotate(Angle)
    
    RD = rangerOffset(1024, Pos, AD)
    AimPos = RD:position()
    
    if( Mouse2 == Press ) {
        
        Target = RD:entity()
        Lock = Target:isValid()
        if( Lock ) {
            if( Gun:hasInput("Fire") ) { Gun["Entity", entity] = Target }
            if( Gun:hasInput("Launch") ) {
                Gun["Entity", entity] = noentity()
                Gun["Vector", vector] = AimPos
            }
        }
        
        if( Gun:hasInput("Lock") ) { Gun["Lock", number] = Lock }
    }
    
    if( Lock & Target ) {
        if( Mouse1 && Current == Current ) {
            if( Gun:hasInput("Fire") )   { Gun["Fire",   number] = On }
            if( Gun:hasInput("Launch") ) { Gun["Launch", number] = On }
        }
    }
}

if( Mouse1 == Release | !Lock | !Target ) {
    if( Gun:hasInput("Fire") )   { Gun["Fire",   number] = Off }
    if( Gun:hasInput("Launch") ) { Gun["Launch", number] = Off }
}

if( Gun:hasInput("Lock") & !( Lock | Target ) ) { Gun["Lock", number] = Off }
