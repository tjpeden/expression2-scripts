@name EGP GUI Elevator
@inputs E:wirelink User:entity Floors:array
@persist LControls:table Info:gtable

if( first() ) { # For performance
    runOnTick(1)
    
    Info = gTable("EGP.GUI")
    
    function number nextID() {
        ID = Info["NextID", number]
        Info["NextID", number] = ID + 1
        return ID
    }
    
    function table newControl(Type:string) {
        Control = table()
        
        Control["Type", string]  = Type
        Control["EGP", wirelink] = E
        
        Control["BorderColor", vector]    = vec(255, 255, 255)
        Control["LabelColor", vector]     = vec(0,   128, 255)
        Control["HighlightColor", vector] = vec(0,   0,   255)
        
        LControls:pushTable(Control)
        return Control
    }
    
    function normal table:ctHitTest(Cursor:vector2) {
        if( !This:exists("TopLeft") ) {
            ContainerID = This["ContainerID", number]
            EGP         = This["EGP", wirelink]
            TopLeft     = EGP:egpPos(ContainerID)
            
            This["TopLeft", vector2]     = TopLeft
            This["BottomRight", vector2] = TopLeft + EGP:egpSize(ContainerID)
        }
        return inrange(Cursor, This["TopLeft", vector2], This["BottomRight", vector2])
    }
    
    function void table:ctSetColor(Object:string, Color:string) {
        This["EGP", wirelink]:egpColor(This[Object, number], This[Color, vector])
    }
    
    function void table:ctHighlight() {
        This:ctSetColor("ContainerID", "HighlightColor")
    }
    
    function void table:ctUnhighlight() {
        This:ctSetColor("ContainerID", "BorderColor")
    }
    
    function void table:ctClicked() {
        dsSendDirect(This["Type", string]+".Clicked", entity(), This)
    }
    
    function void table:cbUpdateCheck() {
        This:ctSetColor("CheckID", (This["Checked", number] ? "CheckedColor" : "UncheckedColor"))
    }
    
    function table newCheckBox(Text:string, Position:vector2, Checked) {
        CheckBox = newControl("CheckBox")
        
        ContainerID = nextID()
        LabelID     = nextID()
        CheckID     = nextID()
        
        E:egpBoxOutline(ContainerID, Position, vec2(30, 30))
        E:egpBox(CheckID, vec2(-10,-10), vec2(20,20))
        E:egpText(LabelID, Text, vec2(30, 0))
        E:egpAlign(LabelID, 0, 1)
        E:egpParent(CheckID, ContainerID)
        E:egpParent(LabelID, ContainerID)
        
        CheckBox["ContainerID", number] = ContainerID
        CheckBox["LabelID", number]     = LabelID
        CheckBox["CheckID", number]     = CheckID
        CheckBox["Text", string]        = Text
        CheckBox["Checked", number]     = Checked
        
        CheckBox["CheckedColor", vector]   = vec(0,   255, 0  )
        CheckBox["UncheckedColor", vector] = vec(0,   0,   0  )
        
        CheckBox:ctSetColor("ContainerID", "BorderColor")
        CheckBox:ctSetColor("LabelID", "LabelColor")
        CheckBox:cbUpdateCheck()
        
        return CheckBox
    }
    
    function void table:cbToggle() {
        This["Checked", number] = !This["Checked", number]
        This:cbUpdateCheck()
    }
    
    # Creates a button where the text is centered inside a Width x 30 rounded box
    function table newButton(Text:string, Position:vector2, Width) {
        Button = newControl("Button")
        
        ContainerID = nextID()
        LabelID     = nextID()
        
        
        E:egpRoundedBoxOutline(ContainerID, Position, vec2(Width,30))
        E:egpText(LabelID, Text, vec2(0,0))
        E:egpAlign(LabelID, 1, 1)
        E:egpParent(LabelID, ContainerID)
        
        # Save defaults to "object"
        Button["ContainerID", number]    = ContainerID
        Button["LabelID", number]        = LabelID
        Button["Text", string]           = Text
        
        Button:ctSetColor("ContainerID", "BorderColor")
        Button:ctSetColor("LabelID", "LabelColor")
        
        return Button
    }
    
    function void newTitle(Text:string) {
        TitleID = nextID()
        E:egpText(TitleID, Text, vec2(256,0))
        E:egpColor(TitleID, vec(0, 0, 255))
        E:egpAlign(TitleID, 1)
        E:egpSize(TitleID, 30)
        
        BorderID = nextID()
        E:egpLine(BorderID, vec2(0,30), vec2(512,30))
        E:egpColor(BorderID, vec(255,0,0))
    }
    
    # Quick function for creating the cursor
    function void newCursor() {
        CursorID = nextID()
        
        E:egpCircle(CursorID, vec2(0,0), vec2(2,2))
        E:egpColor(CursorID, vec(255, 0, 0))
        E:egpParentToCursor(CursorID)
    }
    
    # Utility method on the entity class
    function normal entity:lookingAt(Other:entity) {
        return This:aimEntity() == Other
    }
    
    E:egpClear()
}

if(!Floors) { exit() }

if( ~Floors | first() ) { # first() is an edge case (updating firmware)
    E:egpClear()
    LControls = table()
    
    newTitle("Elevator")
    
    # Floor button code here
    for( I = 1, Floors:maxIndex() ) {
        N = I - 1, X = 256, Y = 32 + (N * 32 + 15)
        Button = newButton(I:toString(), vec2(X,Y), 40)
        Button["Z", number] = Floors[I, number]
    }
    
    newCursor()
}

if( dsClk("Button.Clicked") ) {
    Button = dsGetTable()
    dsSend("Call", "Elevator", Button["Z", number])
}

# Check for and act on player use key events
if( ~User & User ) {
    Cursor = E:egpCursor(User)
    
    for(I = 1, LControls:count()) {
        Control = LControls[I, table]
        if( Control:ctHitTest(Cursor) ) {
            Control:ctClicked()
            break
        }
    }
}
