@name Allowable Door v2
@outputs Fade Allowable:table
@persist Door:entity Env:table Position:vector
@model models/props/cs_militia/wndw01_breakable_frame.mdl

if( first() | duped() ) {
    #include "lib/constants"
    #include "lib/allowable"
    #include "lib/command"
    #include "lib/utils"
    
    runOnTick(1)
    runOnChat(1)
    
    setName(hash():toString())
    entity():isWeldedTo():createWire(entity(), "Fade", "Fade")
    
    function addOwner(Args:table, Ret:table) {
        local Player = owner()
        local TeamColor = teamColor(Player:team())
        
        Allowable:aSetAllowed(Player, 1)
        printColor(Blue, "Added player: ", TeamColor, Player:name())
    }
    
    function addPlayer(Args:table, Ret:table) {
        local Player = Args:popEntity()
        local TeamColor = teamColor(Player:team())
        
        Allowable:aSetAllowed(Player, 1)
        printColor(Blue, "Added player: ", TeamColor, Player:name())
    }
    
    function removePlayer(Args:table, Ret:table) {
        local Player = Args:popEntity()
        local TeamColor = teamColor(Player:team())
        
        Allowable:aSetAllowed(Player, 0)
        printColor(Blue, "Removed player: ", TeamColor, Player:name())
    }
    
    Position = entity():pos()
    
    Allowable = newAllowable()
    
    Env = newEnv(":")
    Env:pushTable(newCommand(":", "addOwner", ""))
    Env:pushTable(newCommand("+", "addPlayer", "p"))
    Env:pushTable(newCommand("-", "removePlayer", "p"))
}

if( tickClk() ) {
    local Player = closestPlayerWithin(Position, 26)
    if( Player & Allowable:aIsAllowed(Player) ) {
        Fade = 1
    } else {
        if( changed(Player) ) {
            printColor(Red, Player:toString())
            owner():soundPlay(0, 0, "garrysmod/content_downloaded.wav")
        }
        Fade = 0
    }
}

if( chatClk(owner()) ) {
    if(Env:envExec(lastSaid())) {
        hideChat(1)
    }
}
